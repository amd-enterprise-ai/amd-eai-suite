# Copyright © Advanced Micro Devices, Inc., or its affiliates.
#
# SPDX-License-Identifier: MIT

*** Settings ***
Documentation       Test scenarios for AIM model API endpoints.
...                 Verifies model listing, retrieval, deletion, and error handling.
...
...                 IMPORTANT: Models are created through finetuning workflows (see finetuning.robot),
...                 not through direct POST /v1/models endpoint (which was removed from the API).
...                 The delete endpoint uses the AIMModel CR name (not a DB UUID).
...                 Pre-created AIMModel CRs via kubectl are used for CPU tests.
Resource    resources/airm_keywords.resource
Resource    resources/airm_projects.resource
Resource    resources/aiwb_models.resource
Resource    resources/aiwb_test_data.resource
Resource    resources/api/common.resource
Test Teardown       Clean Up All Created Models


*** Test Cases ***
AIM model appears in namespace model list
    [Documentation]    Verify that an AIM model appears in the namespace model list.
    [Tags]    models    list    cpu

    Given Project exists in system
    And a dummy AIMModel CR exists in the namespace

    Then the dummy AIMModel should appear in the namespace model list

AIM model is retrievable by resource name
    [Documentation]    Verify that an AIM model can be retrieved by resource name.
    [Tags]    models    get    cpu

    Given Project exists in system
    And a dummy AIMModel CR exists in the namespace

    Then the dummy AIMModel should be retrievable by resource name

Requesting a non-existent AIM model returns not-found error
    [Documentation]    Verify that requesting a non-existent model by resource name returns 404.
    [Tags]    models    error    cpu

    Given Project exists in system

    Then getting a non-existent model should return 404

Finetunable models include GPU hardware compatibility information
    [Documentation]    Verify finetunable models are returned with GPU hardware details
    ...    so users can identify which models are compatible with their cluster's accelerators.
    [Tags]    models    finetunable    smoke

    Given Project exists in system
    When finetunable models list is requested
    Then each finetunable model should include GPU hardware compatibility details

Deleting a non-existent model returns not-found error
    [Documentation]    Verify the API returns a not-found error when attempting to delete
    ...    a model that does not exist.
    [Tags]                  models                  delete                  negative
    Given Project exists in system
    And a model does not exist
    When delete model request is sent
    Then response status should be 404

Delete AIMModel CR via API
    [Documentation]    Verify a fine-tuned model is deleted when the API endpoint is called with its name.
    ...    Tests the full deletion path: API call → AIMModel CR removal from the cluster.
    ...
    ...    Steps:
    ...    1. Pre-create an AIMModel CR directly in the cluster
    ...    2. Call the fine-tuning capability delete endpoint with the CR name
    ...    3. Verify 204 No Content response
    ...    4. Verify the AIMModel CR no longer exists in the cluster
    [Tags]                  models                  delete                  kubectl
    Given Project exists in system
    And an AIMModel CR exists in the namespace
    When delete AIMModel CR request is sent
    Then response status should be 204
    And the AIMModel CR should not exist

# Note: Model creation tests are in finetuning.robot since models are created
# through the fine-tuning capability endpoint (POST /v1/projects/{project}/fine-tuning/jobs),
# not through direct creation. The POST /v1/models endpoint was removed from the API.
